             task ::= [budget] sentence
         sentence ::= statement"." [tense] [truth]
                    | statement"?" [tense]
                    | statement"@" [tense]
                    | statement"!" [truth]
        statement ::= "<"term copula term">"
                    | term
                    | "(^"word {","term} ")"
           copula ::= "-->"
                    | "<->"
                    | "{--"
                    | "--]"
                    | "{-]"
                    | "==>"
                    | "=/>"
                    | "=|>"
                    | "=\\>"
                    | "<=>"
                    | "</>"
                    | "<|>"
             term ::= word
                    | variable
                    | <compound-term>
                    | statement
  <compound-term> ::= "{" term {","term} "}"
                    | "[" term {","term} "]"
                    | "(&," term {","term} ")"
                    | "(|," term {","term} ")"
                    | "(-," term "," term ")"
                    | "(~," term "," term ")"
                    | "(*," term {","term} ")"
                    | "(/," term {","term} ")"
                    | "(\\," term {","term} ")"
                    | "(--," term ")"
                    | "(||," term {","term} ")"
                    | "(&&," term {","term} ")"
                    | "(&/," term {","term} ")"
                    | "(&|," term {","term} ")"
         variable ::= "$"word
                    | "#"[word]
                    | "?"[word]
            tense ::= ":/:"
                    | ":|:"
                    | ":\\:"
            truth ::= "%"frequency[";"confidence]"%"
           budget ::= "$"priority[";"durability]"$"
               word : #"\w+"
           priority : #"[0-9]*(\.[0-9]+)?"
         durability : #"[0-9]*(\.[0-9]+)?"
          frequency : #"[0-9]*(\.[0-9]+)?"
         confidence : #"[0-9]*(\.[0-9]+)?"